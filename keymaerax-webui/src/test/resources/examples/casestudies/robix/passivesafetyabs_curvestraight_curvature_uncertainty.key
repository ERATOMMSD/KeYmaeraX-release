Functions.
  R ep.   /* time limit for control decisions */
  R B.    /* minimum braking capability of the robot */
  R A.    /* maximum acceleration -B <= a <= A */
  R V.    /* maximum velocity of obstacles */
  R Dpr.  /* robot position measurement error */
  R Dv.   /* robot velocity measurement error */
  R Da.
  R Dpo.  /* obstacle position measurement error */
  R abs(R).  /* predefined function of absolute value */
End.

ProgramVariables.
  R x.    /* robot position: x */
  R y.    /* robot position: y */
  R mx.   /* measured robot position: y */
  R my.   /* measured robot position: y */
  R v.    /* robot translational velocity */
  R mv.   /* robot measured velocity */
  R a.    /* robot translational acceleration */
  R pa.   /* robot perturbated acceleration */
  R dx.   /* robot orientation: x */
  R dy.   /* robot orientation: y */
  R w.    /* robot rotational velocity */
  R k.    /* curvature of curve (i)straight: k=0; (ii)curve: k!=0 */
  R xo.   /* position summary of obstacles */
  R yo.
  R mxo.  /* measured obstacle position */
  R myo.
  R dxo.  /* direction and linear velocity of obstacles */
  R dyo.
  R t.    /* time */
End.

Problem.
    v >= 0
  & ( abs(x-xo) > v^2 / (2*Da*B) + V*(v/(Da*B))
    | abs(y-yo) > v^2 / (2*Da*B) + V*(v/(Da*B)))
  & dx^2 + dy^2 = 1
  & A >= 0
  & B > 0
  & V >= 0
  & ep > 0
  & Dpr >= 0
  & Dpo >= 0
  & Dv >= 0
  & 0 < Da & Da <= 1
  ->
  [
    {
      {
        /* control obstacle */
        dxo :=*;
        dyo :=*;
        ?dxo^2 + dyo^2 <= V^2;

        /* take measurements */
        mx:=*; ?-Dpr <= x-mx & x-mx <= Dpr;
        my:=*; ?-Dpr <= y-my & y-my <= Dpr;
        mv:=*; ?-Dv <= v-mv & v-mv <= Dv;

        {
          /* brake on current curve or remain stopped */
          { a := -B; }
          ++
          { ?v = 0; a := 0; w := 0; /* discrete physics: when stopped, stay stopped */ }
      	  ++
    	  /* or choose a new safe curve */
    	  { a:=*; ?-B <= a & a <= A;
          k:=*;
          w:=*; ?v * k = w;

          /* for the chosen a, w, cx, cy: worst case position of obstacles wrt. curve */
          xo:=*;
          yo:=*;

          mxo:=*; ?-Dpo <= xo-mxo & xo-mxo <= Dpo;
          myo:=*; ?-Dpo <= yo-myo & yo-myo <= Dpo;

          /* use that curve, if it is a safe one (admissible velocities) */
          ? abs(mx-mxo) > (mv+Dv)^2/(2*B*Da) + V*(mv+Dv)/(B*Da) + Dpr + Dpo + (A/(B*Da) + 1) * (A/2 * ep^2 + ep*(mv+Dv+V))
          | abs(my-myo) > (mv+Dv)^2/(2*B*Da) + V*(mv+Dv)/(B*Da) + Dpr + Dpo + (A/(B*Da) + 1) * (A/2 * ep^2 + ep*(mv+Dv+V));
    	  }
    	};
    	t := 0;
      }

      /* piecewise actuator perturbation */
      pa:=*; ?Da <= pa & pa <= 1;

      /* dynamics */
      { x' = v * dx,
        y' = v * dy,
        dx' = -w * dy,
        dy' = w * dx,
        v' = a*pa,
        w' = a*pa*k,
        xo' = dxo,
        yo' = dyo,
        t' = 1 & t <= ep & v >= 0
      } /*@invariant(
          t >= 0,
          dx^2 + dy^2 = 1
          v = v0 + a*t,
          -t * (v - a/2*t) <= x - x0 & x - x0 <= t * (v - a/2*t),
          -t * (v - a/2*t) <= y - y0 & y - y0 <= t * (v - a/2*t),
          -t * V <= xo - xo0 & xo - xo0 <= t * V,
          -t * V <= yo - yo0 & yo - yo0 <= t * V
        )*/
    }* /*@invariant(
         v >= 0
         & dx^2+dy^2 = 1
         & (v = 0 | abs(x-xo) > v^2 / (2*B) + V*(v/B) | abs(y-yo) > v^2 / (2*B) + V*(v/B))
       )*/
  ] (v > 0 -> (x - xo)^2 + (y - yo)^2 > 0)
End.